apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: "org.jetbrains.kotlin.android.extensions"


ext.mapfit = [
        'sdk_version'        : '2.1.1',
        'sdk_version_code'   : 15,
        'compile_sdk_version': 27,
        'build_tools_version': '27.0.3',
        'min_sdk_version'    : 16,
        'target_sdk_version' : 27
]

android {
    compileSdkVersion project.mapfit.compile_sdk_version
    buildToolsVersion project.mapfit.build_tools_version

    defaultConfig {
        minSdkVersion project.mapfit.min_sdk_version
        targetSdkVersion project.mapfit.target_sdk_Version
        versionName project.mapfit.sdk_version
        versionCode project.mapfit.sdk_version_code

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        androidExtensions {
            experimental = true
        }

        externalNativeBuild {
            cmake {
                targets 'tangram'
                arguments '-DTANGRAM_PLATFORM=android',
                        '-DANDROID_TOOLCHAIN=clang',
                        '-DANDROID_STL=c++_shared'
                cppFlags '-std=c++14',
                        '-pedantic',
                        '-fPIC',
                        '-fexceptions',
                        '-frtti',
                        //warnings
                        '-Wall',
                        '-Wignored-qualifiers',
                        '-Wtype-limits',
                        '-Wmissing-field-initializers',
                        '-Wno-format-pedantic'

                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86'
            }
        }
    }

    externalNativeBuild {
        cmake {
            path '../mapfit-gl/CMakeLists.txt'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    buildTypes {

        release {
            buildConfigField "Boolean", "DEBUG_MODE", "false"
            externalNativeBuild {
                cmake.cppFlags '-g0'
            }
        }

        debug {
            buildConfigField "Boolean", "DEBUG_MODE", "false"
            externalNativeBuild {
                cmake.cppFlags '-g'
            }
        }

    }

    defaultConfig {
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError false
    }

    kapt { generateStubs = true }

}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    api "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"
    api "org.jetbrains.kotlin:kotlin-reflect:${versions.kotlin}"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.kotlin_coroutine}"
    api "com.android.support:appcompat-v7:${versions.support_lib}"
    api "com.android.support:support-annotations:${versions.support_lib_version}"
    api "com.squareup.okhttp3:okhttp:${versions.okHttp}"
    api "com.squareup.okhttp3:logging-interceptor:${versions.okHttp}"
    api "com.squareup.moshi:moshi-kotlin:${versions.moshi}"

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.json:json:20140107'
    testImplementation "org.mockito:mockito-core:${versions.mockito}"
    testImplementation "org.mockito:mockito-inline:${versions.mockito}"
    testImplementation "com.squareup.okhttp3:mockwebserver:${versions.okHttp}"
    androidTestImplementation "org.mockito:mockito-android:${versions.mockito}"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-idling-resource:3.0.2'
}

repositories {
    mavenCentral()
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

if (project.rootProject.file('mapfit_local.properties').exists()) {
    apply from: file("${rootDir}/gradle/jcenter-push.gradle")
}

apply from: file("${rootDir}/gradle/mapfit-tasks.gradle")
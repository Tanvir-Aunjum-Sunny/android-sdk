apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: "org.jetbrains.kotlin.android.extensions"

project.ext {
    mapfitSdkVersion = '1.0.0'
    mapfitSdkVersionCode = 1
}

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode project.mapfitSdkVersionCode
        versionName project.mapfitSdkVersion

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        androidExtensions {
            experimental = true
        }

        externalNativeBuild {
            cmake {
                targets 'tangram'
                arguments '-DPLATFORM_TARGET=android',
                        '-DANDROID_TOOLCHAIN=clang',
                        '-DANDROID_STL=c++_shared'
                cppFlags '-std=c++14',
                        '-pedantic',
                        '-fPIC',
                        '-fexceptions',
                        '-frtti',
                        //warnings
                        '-Wall',
                        '-Wignored-qualifiers',
                        '-Wtype-limits',
                        '-Wmissing-field-initializers',
                        '-Wno-format-pedantic'

                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86'
            }
        }
    }

    externalNativeBuild {
        cmake {
            path '../mapfit-gl/CMakeLists.txt'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    buildTypes {

        release {
            buildConfigField "Boolean", "DEBUG_MODE", "false"
            externalNativeBuild {
                cmake.cppFlags '-g0'
            }
        }

        debug {
            buildConfigField "Boolean", "DEBUG_MODE", "false"
            externalNativeBuild {
                cmake.cppFlags '-g'
            }
        }

    }

    defaultConfig {
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError false
    }

    kapt { generateStubs = true }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22.2'
    implementation 'org.jetbrains.anko:anko-coroutines:0.10.4'
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:support-annotations:27.0.2'
    implementation 'com.squareup.okhttp3:okhttp:3.9.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'
    implementation 'com.squareup.moshi:moshi-kotlin:1.5.0'

    testImplementation 'junit:junit:4.12'
    testCompile 'org.json:json:20140107'
    testCompile 'org.mockito:mockito-core:2.15.0'
    testCompile 'org.mockito:mockito-inline:2.15.0'
    androidTestImplementation 'org.mockito:mockito-android:2.15.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.9.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}

repositories {
    mavenCentral()
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

apply from: file("${rootDir}/gradle/jcenter-push.gradle")